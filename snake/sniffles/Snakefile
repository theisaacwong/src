import pandas as pd
import os, sys

configfile: "config.yaml"


MANIFEST = config.get("MANIFEST", "manifest.tab")
SNIFFLES_VERSION = config.get("SNIFFLES_VERSION", "2.3.4")
CUTESV_VERSION = config.get("CUTESV_VERSION", "2.1.1")
ANNOTATIONS_TR = config.get("ANNOTATION", "") # tandem repeats
NTHREADS = config.get("NTHREADS", 2)
REF = config.get("REF", "/net/eichler/vol28/eee_shared/assemblies/hg38/no_alt/hg38.no_alt.fa")

manifest_df = pd.read_csv(MANIFEST, sep="\t", index_col="SAMPLE")

# TODO: add lambda funcitons, esp for restarts with more memory, 
# TODO: add pbsv and 1 compression rule for everything, 

def get_bam(wildcards):
    return manifest_df.at[wildcards.sample, "BAM"]

localrules:
    all,


rule all:
    input:
        expand(
            "results/{sample}/sniffles/{sniffles_ver}/{sample}.sniffles.vcf.gz",
            sample=manifest_df.index,
            sniffles_ver=SNIFFLES_VERSION
        ),
        expand(
            "results/{sample}/sniffles/{sniffles_ver}/{sample}.sniffles.vcf.gz.tbi",
            sample=manifest_df.index,
            sniffles_ver=SNIFFLES_VERSION
        ),
        expand(
            "results/{sample}/cutesv/{cutesv_ver}/{sample}.cutesv.vcf.gz",
            sample=manifest_df.index,
            cutesv_ver=CUTESV_VERSION
        ),
        expand(
            "results/{sample}/cutesv/{cutesv_ver}/{sample}.cutesv.vcf.gz.tbi",
            sample=manifest_df.index,
            cutesv_ver=CUTESV_VERSION
        ),




rule sniffles:
    input: 
        bam=get_bam,
    output: 
        vcf="results/{sample}/sniffles/{sniffles_ver}/{sample}.sniffles.vcf",
    threads: NTHREADS,
    resources:
        mem=8,
        hrs=24,
    singularity:
        "docker://eichlerlab/sniffles:2.3.2",
    shell:
        """
        mkdir -p results/{wildcards.sample}/sniffles/{wildcards.sniffles_ver}/
        sniffles -i {input.bam} --tandem-repeats {ANNOTATIONS_TR} --threads {threads} --reference {REF} -v {output.vcf}
        """

rule cutesv:
    input: 
        bam=get_bam,
    output: 
        vcf="results/{sample}/cutesv/{cutesv_ver}/{sample}.cutesv.vcf",
    threads: NTHREADS,
    resources:
        mem=8,
        hrs=24,
    singularity:
        "docker://eichlerlab/cutesv:2.1.1",
    shell:
        """
        mkdir -p results/{wildcards.sample}/cutesv/{wildcards.cutesv_ver}/
        cuteSV --threads {threads} {input.bam} {REF} {output.vcf} results/{wildcards.sample}/cutesv/{wildcards.cutesv_ver}/
        """
        
        
rule compress:
    input: 
        vcf="results/{sample}/{software}/{version}/{sample}.{software}.vcf",
    output: 
        vcf="results/{sample}/{software}/{version}/{sample}.{software}.vcf.gz",
        index="results/{sample}/{software}/{version}/{sample}.{software}.vcf.gz.tbi",
    threads: 2,
    resources:
        mem=4,
        hrs=24,
    singularity:
        "docker://eichlerlab/cutesv:2.1.1",
    shell:
        """
        bgzip -@ {threads} {input.vcf}
        tabix -p vcf {output.vcf}
        """